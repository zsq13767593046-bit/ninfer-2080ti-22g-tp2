#include "ops/linear_swiglu/fp8/fp8_linear_swiglu_plan.h"

#include "core/device.h"
#include "ops/launcher/kernel_attr_once.h"
#include "ops/linear/fp8/fp8_a8_mma.cuh"
#include "ops/linear/fp8/fp8_a8_plan.h"
#include "ops/linear/fp8/fp8_a8_schedule.cuh"
#include "ops/linear/fp8/fp8_config.h"
#include "ops/linear/fp8/fp8_output.cuh"
#include "ops/linear_swiglu/fp8/fp8_linear_swiglu_output.cuh"

#include <cuda_bf16.h>

#include <cstdint>

namespace ninfer::ops::detail {
namespace {

template <class Geometry, bool FullTokens>
void launch_mma(const Weight& weight, Tensor& out, Fp8A8Workspace workspace, std::int32_t tokens,
                cudaStream_t stream) {
    using Schedule               = typename Fp8LinearA8ProductionSchedule<Geometry>::Type;
    constexpr int kIntermediate = Geometry::kOutputRows / 2;
    using Rows                  = Fp8SwiGluRows<Schedule::kBlockRows / 2, kIntermediate>;
    static_assert((Schedule::kBlockRows % 2) == 0);

    constexpr int kRowTiles = Geometry::kOutputRows / Schedule::kBlockRows;
    const int token_tiles   = (tokens + Schedule::kBlockTokens - 1) / Schedule::kBlockTokens;
    const int blocks        = kRowTiles * token_tiles;
    const Rows rows{};
    const Fp8SwiGluOutput output{static_cast<__nv_bfloat16*>(out.data), kIntermediate};

    if constexpr (Schedule::kSharedBytes > 48 * 1024) {
        ensure_func_attr_per_device(
            fp8_mma_kernel<Geometry, Schedule, FullTokens, Fp8IdentityEpilogue, Fp8SwiGluOutput,
                           Rows, true>,
            cudaFuncAttributeMaxDynamicSharedMemorySize, Schedule::kSharedBytes);
    }
    fp8_mma_kernel<Geometry, Schedule, FullTokens, Fp8IdentityEpilogue, Fp8SwiGluOutput, Rows, true>
        <<<blocks, Schedule::kThreads, Schedule::kSharedBytes, stream>>>(
            workspace.codes, workspace.scales, static_cast<const std::uint8_t*>(weight.qdata),
            static_cast<const __nv_bfloat16*>(weight.scales), tokens, Fp8IdentityEpilogue{}, output,
            rows);
    CUDA_CHECK(cudaGetLastError());
}

template <class Geometry>
void launch_a8(const Tensor& x, const Weight& weight, Tensor& out, WorkspaceArena& workspace,
              cudaStream_t stream) {
    using Schedule = typename Fp8LinearA8ProductionSchedule<Geometry>::Type;
    auto scope     = workspace.scope();
    const Fp8A8Workspace scratch =
        allocate_fp8_a8_workspace(workspace, x.ne[1], Geometry::kInputRows);
    launch_fp8_a8_quantize(x, weight, scratch, stream);
    if ((x.ne[1] % Schedule::kBlockTokens) == 0) {
        launch_mma<Geometry, true>(weight, out, scratch, x.ne[1], stream);
    } else {
        launch_mma<Geometry, false>(weight, out, scratch, x.ne[1], stream);
    }
}

} // namespace

void fp8_linear_swiglu_a8_launch(const Tensor& x, const Weight& weight, Tensor& out,
                                 WorkspaceArena& workspace, cudaStream_t stream) {
    launch_a8<Fp8MlpGateUpGeometry>(x, weight, out, workspace, stream);
}

// linear_swiglu's own tp2 column shard.
void fp8_linear_swiglu_a8_launch_shard(const Tensor& x, const Weight& weight, Tensor& out,
                                       WorkspaceArena& workspace, cudaStream_t stream) {
    launch_a8<Fp8MlpGateUpTp2ColumnGeometry>(x, weight, out, workspace, stream);
}

} // namespace ninfer::ops::detail
